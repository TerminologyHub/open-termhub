buildscript {
  ext {
    springBootVersion = "3.4.10"
  }
  repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://repo.spring.io/release" }
    maven { url "https://repo.spring.io/milestone" }
    maven { url "https://repo.spring.io/snapshot" }
    maven { url "https://plugins.gradle.org/m2/" }
    gradlePluginPortal()
  }

  dependencies {
    classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
  }
}

// This must be at the top of the file (only can be below buildscript)
plugins {
    id 'java'
    id 'eclipse'
    id 'war'
    id 'org.springframework.boot' version '3.4.10'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'com.github.hierynomus.license' version '0.16.1'
    id 'com.github.spotbugs' version '6.0.26'
    id 'jacoco'
    id 'checkstyle'
    id 'maven-publish'
}

repositories {
  mavenLocal()
  mavenCentral()
  maven { url "https://repo.spring.io/release" }
  maven { url "https://repo.spring.io/milestone" }
  maven { url "https://repo.spring.io/snapshot" }
}

group = 'com.wci.open-termhub'
version = '1.1.8-202510'

sourceCompatibility = 17
targetCompatibility = 17

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations.all {
   exclude group: "org.springframework.boot", module: "spring-boot-starter-logging"
}

// Ensure Spring Boot's dependency management is explicitly configured
dependencyManagement {
//    imports {
//        mavenBom "org.springframework.boot:spring-boot-dependencies:3.4.9"
//    }
    dependencies {
        dependency('org.fhir:ucum:1.0.9')

//        // NOTE: when spring version is updated, do NOT explicily do this
//        // It was only done to handle trivy vulnerability without upgrading to 3.5 boot
//        dependency('org.springframework:spring-core:6.2.11')
//        dependency('org.springframework:spring-context:6.2.11')
    }
}

dependencies {
  // NOTE: "spring-boot-starter-web" must be the first dependency
  implementation "org.springframework.boot:spring-boot-starter-web"
  implementation "org.springframework.boot:spring-boot-starter-actuator"
  implementation "org.springframework.boot:spring-boot-starter-log4j2"

  implementation 'org.springframework:spring-core:6.2.11'

  // Vulnerability import
  //implementation "org.apache.tomcat.embed:tomcat-embed-core:10.1.44"

  // prometheus
  implementation "io.micrometer:micrometer-registry-prometheus"

  // Add ANTLR runtime dependency to match code generation version
  implementation 'org.antlr:antlr4-runtime:4.13.2'

  // Docs: https://docs.spring.io/spring-data/elasticsearch/docs/4.2.12/reference/html/#reference
  implementation 'org.springframework.data:spring-data-elasticsearch:5.3.11'
  implementation 'org.apache.lucene:lucene-core:9.11.1'
  implementation 'org.apache.lucene:lucene-analysis-common:9.11.1'
  implementation 'org.apache.lucene:lucene-queryparser:9.11.1'
  implementation 'org.apache.lucene:lucene-join:9.11.1'
  implementation 'org.apache.lucene:lucene-queries:9.11.1'

  // REST
  implementation 'jakarta.ws.rs:jakarta.ws.rs-api:4.0.0'

  // OpenAPI/Swagger UI - Using HAPI FHIR's built-in support
  implementation ("org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.14") {
    // Remove slf4j-api 2+
    exclude group: "org.slf4j", module: "slf4j-simple"
    exclude group: "org.slf4j", module: "slf4j-api"
  }

  implementation 'org.apache.commons:commons-text:1.12.0'
  implementation 'org.apache.commons:commons-math3:3.6.1'
  implementation 'org.springframework.boot:spring-boot-starter-aop'
  implementation 'org.aspectj:aspectjweaver:1.9.22'

  implementation 'org.reflections:reflections:0.10.2'
  implementation ('com.google.guava:guava:33.4.8-jre') {
	  attributes{
	    attribute(Attribute.of('org.gradle.jvm.environment', String),'standard-jvm')
	  }
  }


  // FHIR libraries
  // The guava exclusion is to avoid things like
  // - ...Could not resolve com.google.guava:guava:32.0.1-jre
  implementation ("ca.uhn.hapi.fhir:hapi-fhir-base:7.6.1") {
     exclude group: "com.google.guava", module: "guava"
     exclude group: "com.fasterxml.jackson.core", module: "jackson-core"
     exclude group: "com.fasterxml.jackson.core", module: "jackson-databind"
     exclude group: "com.fasterxml.jackson.core", module: "jackson-annotations"
  }
  implementation ("ca.uhn.hapi.fhir:hapi-fhir-structures-r4:7.6.1") {
     exclude group: "com.google.guava", module: "guava"
  }
  implementation ("ca.uhn.hapi.fhir:hapi-fhir-server:7.6.1") {
     exclude group: "com.google.guava", module: "guava"
  }
  implementation ("ca.uhn.hapi.fhir:hapi-fhir-jpaserver-base:7.6.1") {
     exclude group: "ca.uhn.hapi.fhir", module: "hapi-fhir-sql-migrate"
     exclude group: "org.elasticsearch.client", module: "elasticsearch-rest-high-level-client"
     exclude group: "org.hibernate.search"
     exclude group: "org.apache.lucene"
     exclude group: "com.github.jsonld-java", module: "jsonld-java"
     exclude group: "com.graphql-java", module: "graphql-java"
     exclude group: "org.glassfish", module: "javax.el"
     exclude group: "org.apache.logging.log4j", module: "log4j-to-slf4j"
     exclude group: "com.google.guava", module: "guava"
     exclude group: "org.apache.jena"
  }
  implementation ("ca.uhn.hapi.fhir:hapi-fhir-server-openapi:7.6.1") {
     exclude group: "com.google.guava", module: "guava"
     exclude group: "org.springdoc", module: "springdoc-openapi-starter-webmvc-ui"
     exclude group: "org.webjars", module: "swagger-ui"
  }
  implementation ("ca.uhn.hapi.fhir:hapi-fhir-structures-dstu2:7.6.1") {
     exclude group: "com.google.guava", module: "guava"
  }
  implementation ("ca.uhn.hapi.fhir:hapi-fhir-validation:7.6.1") {
     exclude group: "com.google.guava", module: "guava"
  }

  // For Test
  testImplementation 'org.springframework.boot:spring-boot-starter-test'
  testImplementation 'org.junit.platform:junit-platform-suite-api:1.10.5'
  testImplementation 'org.junit.platform:junit-platform-suite-engine:1.10.5'
}

// ./gradlew clean test
test {
    useJUnitPlatform()
    exclude '**/integrationtest/**'
    maxParallelForks = 1
    testLogging {
        events "skipped", "failed"
        exceptionFormat "full"
        showStandardStreams = true
        showCauses = true
        showExceptions = true
        showStackTraces = true
    }
}

// ./gradlew clean testR4
task testR4(type: Test) {
    useJUnitPlatform()
    include '**/r4/**'
    testLogging {
        events "skipped", "failed"
        exceptionFormat "full"
        showStandardStreams = true
        showCauses = true
        showExceptions = true
        showStackTraces = true
    }
}

// ./gradlew clean testR5
task testR5(type: Test) {
    useJUnitPlatform()
    include '**/r5/**'
    testLogging {
        events "skipped", "failed"
        exceptionFormat "full"
        showStandardStreams = true
        showCauses = true
        showExceptions = true
        showStackTraces = true
    }
}

// use "gradle licenseFormatMain/Test" to automatically generate headers
license {
  header = rootProject.file("config/LICENSE")
  strictCheck true
  encoding = "UTF-8"
  mapping("java", "SLASHSTAR_STYLE")
  includes(["**/*.java"])
  excludes(["**/*.txt", "**/*.conf", "**/*package-info.java", "**/*.properties"])
  ext.year = Calendar.getInstance().get(Calendar.YEAR)
  ext.company = "West Coast Informatics"
}
licenseTest.dependsOn licenseFormatTest
licenseMain.dependsOn licenseFormatMain

// SpotBugs https://spotbugs.readthedocs.io/en/latest/gradle.html
// https://github.com/spotbugs/spotbugs-gradle-plugin
spotbugs {
  ignoreFailures = false
  showStackTraces = true
  showProgress = true
  excludeFilter = file("${rootDir}/config/spotbugs/excludeFilter.xml")
}
spotbugsMain {
    reports {
        html {
            enabled = true
            outputLocation = file("$buildDir/reports/spotbugs/main/spotbugs.html")
            stylesheet = 'fancy-hist.xsl'
        }
    }
}
spotbugsTest {
    reports {
        html {
            enabled = true
            outputLocation = file("$buildDir/reports/spotbugs/main/spotbugs.html")
            stylesheet = 'fancy-hist.xsl'
        }
    }
}

// Checkstyle
checkstyle {
  toolVersion '10.25.0'
  ignoreFailures = true
  // Path to your Checkstyle configuration file
  configFile = file("${rootDir}/config/checkstyle/checkstyle.xml")
}
checkstyleMain {
  reports {
    html.destination = file("$buildDir/reports/checkstyle.html")
  }
}
checkstyleTest {
  reports {
    html.destination = file("$buildDir/reports/checkstyle.html")
  }
}

tasks.withType(JavaExec) {
  if (System.getProperty('DEBUG', 'false') == 'true') {
    jvmArgs '-Xdebug', '-Xrunjdwp:transport=dt_socket,server=y,suspend=y,address=5005'
  }
}

// Specify "export JAVA_OPTS=-Xmx8g" to set upper memory bound for upload
bootRun {
    if (project.hasProperty('debug')) {
        jvmArgs = ["-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=5005"]
    }
    // Check if the JAVA_OPTS environment variable is set and not empty
    if (System.getenv('JAVA_OPTS')) {
        // Use a simple loop to parse the arguments from the env variable
        def opts = System.getenv('JAVA_OPTS').split()
        for (arg in opts) {
            jvmArgs arg
        }
    }
    // args = ['--spring.profiles.active=your-profile']
}

bootJar {
  mainClass = 'com.wci.termhub.Application'
}

jacoco {
  toolVersion = "0.8.11"
}

jacocoTestReport {
  dependsOn test

  reports {
    html {
      required = true
      destination file("${buildDir}/reports/jacoco")
    }
    xml {
      required = true
      destination file("${buildDir}/reports/jacoco/report.xml")
    }
    csv.required = false
  }
}

jacocoTestCoverageVerification {
  violationRules {
    rule {
      element = 'CLASS'
      limit {
        counter = 'LINE'
        value = 'COVEREDRATIO'
        minimum = 0.60
      }
      excludes = [
        'com.wci.termhub.Application',
        'com.wci.termhub.config.*',
        'com.wci.termhub.model.*'
      ]
    }
  }
}

// To enact - ./gradlew dependencies --write-locks
dependencyLocking {
    lockAllConfigurations() // Lock all configurations by default
}
